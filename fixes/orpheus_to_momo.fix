# id
marc_map(999c, r.id)
prepend(r.id, 'orpheus:')
cp(r.id, r._id)

# collection
add(r.collection.$append, orpheus)

# title
marc_map(245a, r.title)

# authors
do marc_each()
    marc_map(100a, r.metadata.author.$append.name)
end

# edition
marc_map(250, r.metadata.edition, join: " ")

# placeOfPublication
marc_map(260a, r.metadata.placeOfPublication)

# publisher
marc_map(260b, r.metadata.publisher)

# publicationDate
marc_map(260c, r.metadata.publicationDate)

# note
do marc_each()
    marc_map(500a, r.metadata.note.$append.text)
end
set(r.metadata.note.*.lang, und)

# iiif manifest
if any_equal(r.id, 'orpheus:105')
    add(r.metadata.iiif.manifest, 'https://sharedcanvas.be/IIIF/manifests/315E3B36-453B-11EB-A063-6F5AACE1713C')
end

# ensure metadata hash exists
unless exists(r.metadata)
    set_hash(r.metadata)
end

# source
use(momo, as: momo)
add(r.source.0.metadata_format, marc-in-json)
momo.marc_in_json()
cp(record, r.source.0.metadata)

# recs
cp(r, recs.$append)
retain(recs)